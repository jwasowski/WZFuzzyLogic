// Block definition (there may be more than one block per file)
FUNCTION_BLOCK salaryRating

// Define input variables
VAR_INPUT
    skill : REAL;
    experience : REAL;
    softSkills : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    salary : REAL;
END_VAR

// Fuzzify input variable 'skill'
// Skill rating based on job requirements 
FUZZIFY skill
    TERM junior := (0, 1) (5, 0) ;
    TERM mid := (1, 0) (4,1) (6,1) (9,0);
    TERM senior := (5, 0) (8, 1) (10,1);
END_FUZZIFY

// Fuzzify input variable 'experience'
// Years of Experience
FUZZIFY experience
    TERM juniorExp := (0, 1) (3,0) ;
    TERM midExp := (1,0) (2.5,1) (4,1) (5,0) ;
    TERM seniorExp := (3, 0) (9,1) (20, 1) ;
END_FUZZIFY

// Fuzzify input variable 'soft skill'
// How good communicator she/he is?
FUZZIFY softSkills
    TERM low := (0, 1) (3,0) ;
    TERM average := (2,0) (5,1) (8,0);
    TERM good := (7,0) (10,1);
END_FUZZIFY

// Defzzzify output variable 'salary'
DEFUZZIFY salary
    TERM low := (2,0) (4,1) (5,0);
    TERM average := (5,0) (7,1) (9,0);
    TERM high := (9,0) (10.5,1) (12,0);
    TERM veryHigh := (12,0) (19,1) (25,0);
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF skill IS junior OR experience IS juniorExp AND softSkills IS low
                THEN salary IS low;

    RULE 2 : IF skill IS mid OR experience IS midExp AND softSkills IS average
                THEN salary IS average;

    RULE 3 : IF skill IS senior AND experience IS seniorExp AND softSkills IS NOT good
                THEN salary IS high;

    RULE 4 : IF skill IS senior AND experience IS seniorExp AND softSkills IS good
                THEN salary IS veryHigh;

    RULE 5 : IF skill IS junior AND experience IS midExp AND softSkills IS good
                THEN salary IS average;

    RULE 6 : IF skill IS junior THEN salary IS low;

    RULE 7 : IF skill IS mid THEN salary IS average;

    RULE 8 : IF skill IS senior THEN salary IS veryHigh;

    RULE 9 : IF softSkills IS low THEN salary IS low;

    RULE 10 : IF softSkills IS good THEN salary IS high;

    RULE 11: IF softSkills IS average THEN salary IS average;

    RULE 12 : IF experience IS juniorExp THEN salary IS low;

    RULE 13 : IF experience IS midExp THEN salary IS average;

    RULE 14 : IF experience IS seniorExp THEN salary IS high;

    RULE 15 : IF experience IS juniorExp AND skill IS mid THEN salary IS average;

    RULE 16 : IF experience IS juniorExp AND skill IS senior THEN salary IS high;

    RULE 17 : IF experience IS midExp AND skill IS junior THEN salary IS low;

    RULE 19 : IF experience IS seniorExp AND skill IS junior THEN salary IS average;

    RULE 20 : IF skill IS mid AND experience IS seniorExp THEN salary IS high;

    RULE 21 : IF softSkills IS good AND skill IS junior AND experience IS juniorExp THEN salary IS low;

    RULE 22 : IF softSkills IS average AND skill IS junior AND experience IS juniorExp THEN salary IS low;

END_RULEBLOCK

END_FUNCTION_BLOCK